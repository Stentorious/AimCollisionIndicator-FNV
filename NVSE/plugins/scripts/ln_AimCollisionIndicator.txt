; Aim Collision Indicator - Stentorious

; On Game Restart
if Goo1.AuxVarGetFlt "*AimCol_Init" 0 != 1
	Goo1.AuxVarSetFlt "*AimCol_Init" 1 0

	; Check for requirements
	if GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 2
	else
		MessageBoxEx "Aim Collision Indicator missing requirement!%rInstall xNVSE 6.3.3+."
		return
	endif
	if GetPluginVersion "JohnnyGuitarNVSE" < 390
		MessageBoxEx "Aim Collision Indicator missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
		return
	endif
	if GetPluginVersion "UI Organizer Plugin" < 230
		MessageBoxEx "Aim Collision Indicator missing requirement!%rInstall latest User Interface Organizer plugin."
		return
	endif
	Goo1.AuxVarSetFlt "*AimCol_Init" 1 1

	; Load INI settings
	SetUIFloatAlt "HUDMainMenu\AimCollision\_Scale" (GetMaxOf 0.5 (GetMinOf 2 (GetINIFloat "Indicator:fScale" "Stentorious\AimCollisionIndicator.ini")))
	SetUIFloatAlt "HUDMainMenu\AimCollision\_VisHip1st" (eval GetINIFloat "Visibility:bHipFire1st" "Stentorious\AimCollisionIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\AimCollision\_VisAim1st" (eval GetINIFloat "Visibility:bAiming1st" "Stentorious\AimCollisionIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\AimCollision\_VisHip3rd" (eval GetINIFloat "Visibility:bHipFire3rd" "Stentorious\AimCollisionIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\AimCollision\_VisAim3rd" (eval GetINIFloat "Visibility:bAiming3rd" "Stentorious\AimCollisionIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\AimCollision\_VisScope" (eval GetINIFloat "Visibility:bScoped" "Stentorious\AimCollisionIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\AimCollision\_VisActor" (eval GetINIFloat "Visibility:bActor" "Stentorious\AimCollisionIndicator.ini")
	SetUIFloatAlt "HUDMainMenu\AimCollision\_DistMax" ((GetMaxOf 0.5 (GetMinOf 5 (GetINIFloat "Visibility:fDistance" "Stentorious\AimCollisionIndicator.ini"))) * 69.99104)

	; Set icon texture
	if GetINIFloat "Indicator:iType" "Stentorious\AimCollisionIndicator.ini" == 0
		SetUIStringAlt "HUDMainMenu\AimCollision\filename" "Interface\Stentorious\AimCollisionIndicator\O.dds"
	else
		SetUIStringAlt "HUDMainMenu\AimCollision\filename" "Interface\Stentorious\AimCollisionIndicator\X.dds"
	endif

	; Set icon color
	string_var sINI = GetINIString "Indicator:iColor" "Stentorious\AimCollisionIndicator.ini"
	int iSystem = 1
	int iLength = Sv_Length sINI
	if eval iLength == 6 && TestExpr (ToNumber sINI 1 1)
		SetUIFloatAlt "HUDMainMenu\AimCollision\red" (ToNumber (sINI[0:1]) 1)
		SetUIFloatAlt "HUDMainMenu\AimCollision\green" (ToNumber (sINI[2:3]) 1)
		SetUIFloatAlt "HUDMainMenu\AimCollision\blue" (ToNumber (sINI[4:5]) 1)
		iSystem = 0
	elseif eval iLength == 1 && TestExpr (ToNumber sINI 0 1)
		iSystem = ToNumber sINI
	endif
	SetUIFloatAlt "HUDMainMenu\AimCollision\systemcolor" iSystem

	Sv_Destruct sINI

endif

; Requirements met
if eval Goo1.AuxVarGetFlt "*AimCol_Init" 1 == 1

	; Hide indicator
	SetUIFloatAlt "HUDMainMenu\AimCollision\_Dist" 10000

	; Update model on load
	Call (CompileScript "AimCollisionIndicator\OnEquip.gek") Player (Player.GetEqObj 5)

	; Update indictor on (un)equip
	SetEventHandlerAlt "OnEquip" (CompileScript "AimCollisionIndicator\OnEquip.gek") 1::Player
	SetEventHandlerAlt "OnUnequip" (CompileScript "AimCollisionIndicator\OnUnequip.gek") 1::Player

endif